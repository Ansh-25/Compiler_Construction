<program> -> <moduleDeclarations> <otherModules> <driverModule> <otherModules>
<moduleDeclarations> ->  <moduleDeclaration> <moduleDeclarations> | ε
<moduleDeclaration> -> DECLARE MODULE ID SEMICOL
<otherModules> -> <module><otherModules>| ε
<driverModule> -> DRIVERDEF DRIVER PROGRAM DRIVERENDDEF <moduleDef>\
<module> -> DEF MODULE ID ENDDEF TAKES INPUT SQBO <input_plist> SQBC SEMICOL <ret><moduleDef>
<ret> -> RETURNS SQBO <output_plist> SQBC SEMICOL | ε

<input_plist> -> ID COLON <dataType> <_input_plist>
<_input_plist> -> COMMA ID COLON <dataType> <_input_plist> | ε
<output_plist> -> ID COLON <type> <_output_plist>
<_output_plist> -> COMMA ID COLON <type> <_output_plist> | ε

<dataType> -> INTEGER | REAL | BOOLEAN | ARRAY SQBO <range> SQBC OF <type>
<type> -> INTEGER | REAL | BOOLEAN

<moduleDef> -> START <statements> END
<statements> -> <statement> <statements> | ε
<statement> -> <ioStmt>|<simpleStmt>|<declareStmt>|<conditionalStmt>|<iterativeStmt>

<ioStmt> -> GET_VALUE BO ID BC SEMICOL | PRINT BO <printable> BC SEMICOL
<var> -> ID <whichId> | NUM | RNUM
<whichId> -> SQBO <index> SQBC | ε
<printable> -> ID <whichId> | NUM | RNUM | TRUE | FALSE

//remove simpleStmt?
<simpleStmt> -> <assignmentStmt> | <moduleReuseStmt>

<assignmentStmt> -> ID <whichStmt>
<whichStmt> -> <lvalueIDStmt> | <lvalueARRStmt>
<lvalueIDStmt> -> ASSIGNOP <expression> SEMICOL
<lvalueARRStmt> -> SQBO <index> SQBC ASSIGNOP <expression> SEMICOL
<index> -> NUM | ID

<moduleReuseStmt> -> <optional> USE MODULE ID WITH PARAMETERS <idList> SEMICOL
<optional> -> SQBO <idList> SQBC ASSIGNOP | ε

<idList> -> ID <_idList>
<_idList> -> COMMA ID <_idlist> | ε

<expression> -> <relationalExpression> <_expression>
<_expression> -> <logicalOP> <relationalExpression> | ε
<relationalExpression> -> <arithmeticExpr> <_relationalExpression> | TRUE | FALSE
<_relationalExpression> -> <relationalOp> <arithmeticExpr> | ε
<arithmeticExpr> -> <term> <_arithmeticExpr> | <addop> <term> <_arithmeticExpr>
<_arithmeticExpr> -> <addop> <term> <_arithmeticExpr> | ε
<term> -> <factor> <_term>
<_term> -> <mulop> <factor> <_term> | ε
<factor> -> BO <expression> BC
<factor> -> <var>

<logicalOp>  ->AND  | OR 
<relationalOp> -> LT | LE | GT | GE | EQ | NE 
<addop> -> PLUS | MINUS
<mulop> -> MUL | DIV


<declareStmt> -> DECLARE <idList> COLON <dataType> SEMICOL

<conditionalStmt> -> SWITCH BO ID <whichId> BC START CASE <value> COLON <statements> BREAK SEMICOL <caseStmt> <default> END
<caseStmt> -> CASE <value> COLON <statements> BREAK SEMICOL <caseStmt> | ε
<value> -> NUM | TRUE | FALSE
<default> -> DEFAULT COLON <statements> BREAK SEMICOL | ε

<iterativeStmt> -> FOR BO ID IN <range> BC START <statements> END | WHILE BO <booleanExpr> BC START <statements> END

<range> -> NUM RANGEOP NUM